// Create a new Ada instance.
function new() {
    ada := {
        "knowledge": [],
        "reflections": {
            "am": "are",
            "was": "were",
            "i": "you",
            "i'd": "you would",
            "i've": "you have",
            "i'll": "you will",
            "my": "your",
            "are": "am",
            "you've": "I have",
            "you'll": "I will",
            "you're": "I am",
            "your": "my",
            "yours": "mine",
            "you": "I",
            "me": "you"
        }
    }

    // Send a greeting response.
    ada.greet := function() {
        return ada.respond("hello")
    }

    // Respond to user input.
    ada.respond := function(text) {
        foundMatch := false

        // Loop through our collect knowledge base, looking for a match against
        // the user's input.
        for (knowledge in ada.knowledge) {
            if ((knowledge.pattern).matches(text.toLowerCase()) and foundMatch == false) {
                foundMatch := true
                matches    := (knowledge.pattern).findAll(text.toLowerCase())
                response   := knowledge.responses[math.random((knowledge.responses).length())]

                for (index, match in matches) {
                    response := response.replace("{" + index + "}", ada.reflect(match))
                }

                print("== " + response)
            }
        }
    }

    // Load in our knowledge base.
    ada.load := function(knowledge) {
        ada.knowledge := knowledge
    }

    // Reflect on the user's input.
    ada.reflect := function(text) {
        words := text.split(" ")

        for (index, word in words) {
            if (ada.reflections[word]) {
                words[index] := ada.reflections[word]
            }
        }

        return words.join(" ")
    }

    return ada
}