function new() {
    ada := {
        "knowledge": [],
        "reflections": {
            "am": "are",
            "was": "were",
            "i": "you",
            "i'd": "you would",
            "i've": "you have",
            "i'll": "you will",
            "my": "your",
            "are": "am",
            "you've": "I have",
            "you'll": "I will",
            "your": "my",
            "yours": "mine",
            "you": "me",
            "me": "you"
        }
    }

    ada.greet := function() {
        return ada.respond("hello")
    }

    ada.respond := function(text) {
        foundMatch := false

        for (knowledge in ada.knowledge) {
            if ((knowledge.pattern).matches(text.toLowerCase()) and foundMatch == false) {
                foundMatch := true
                matches    := (knowledge.pattern).findAll(text.toLowerCase())
                response   := knowledge.responses[math.random((knowledge.responses).length())]

                for (index, match in matches) {
                    response := response.replace("{" + index + "}", ada.reflect(match))
                }

                print("== " + response)
            }
        }
    }

    ada.load := function(knowledge) {
        ada.knowledge := knowledge
    }

    ada.reflect := function(text) {
        words := text.split(" ")

        for (index, word in words) {
            if (ada.reflections[word]) {
                words[index] := ada.reflections[word]
            }
        }

        return words.join(" ")
    }

    return ada
}